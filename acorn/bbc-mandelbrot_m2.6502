ORG $2000

.start
   jmp main

INCLUDE "mandelbrot.6502"

ZP_PTR            = $50
ZP_SCRATCH        = $52

CRTC_REG_ADDR     = $FE00
CRTC_REG_DATA     = $FE01
VIDEO_ULA_CTRL    = $FE20
VIDEO_ULA_PAL     = $FE21
SCREEN_MAP        = $4000

.i_result EQUB 0

.color_codes
   EQUB $3F,$03,$0C,$0F   ; white, red, green, yellow
   EQUB $30,$33,$3C       ; blue, magenta, cyan
   EQUB $3F,$03,$0C,$0F   ; white, red, green, yellow
   EQUB $30,$33,$3C,$00   ; blue, magenta, cyan, black

.*main
{
   lda #12
   sta CRTC_REG_ADDR
   lda #>(SCREEN_MAP/8)
   sta CRTC_REG_DATA
   lda #13
   sta CRTC_REG_ADDR
   lda #<(SCREEN_MAP/8)
   sta CRTC_REG_DATA
   ldx #0
   ldy #0
.loop
   lda mand_max_it
   jsr mand_get
   sta i_result
   txa
   pha ; preserve X
   tya
   pha ; preserve Y
   lda #<SCREEN_MAP
   sta ZP_PTR
   lda #>SCREEN_MAP
   sta ZP_PTR+1
   tya
   sta ZP_SCRATCH
   lda #0
   sta ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   lda ZP_PTR
   clc
   adc ZP_SCRATCH
   sta ZP_PTR
   lda ZP_PTR+1
   adc ZP_SCRATCH+1
   sta ZP_PTR+1      ; ZP_PTR = SCREEN_MAP+Y*$80
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   lda ZP_PTR
   clc
   adc ZP_SCRATCH
   sta ZP_PTR
   lda ZP_PTR+1
   adc ZP_SCRATCH+1
   sta ZP_PTR+1      ; ZP_PTR = SCREEN_MAP+Y*$280
   txa
   sta ZP_SCRATCH
   lda #0
   sta ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   asl ZP_SCRATCH
   rol ZP_SCRATCH+1
   lda ZP_PTR
   clc
   adc ZP_SCRATCH
   sta ZP_PTR
   lda ZP_PTR+1
   adc ZP_SCRATCH+1
   sta ZP_PTR+1      ; ZP_PTR = SCREEN_MAP+Y*$280+X*$10
   ldx i_result
   lda color_codes,x
   ldy #0
.pix_loop
   sta (ZP_PTR),y
   iny
   cpy #16
   bne pix_loop
   pla
   tay ; restore Y
   pla
   tax ; restore X
   inx
   cpx mand_width
   beq nexty
   jmp loop
.nexty
   ldx #0
   iny
   cpy mand_height
   beq done
   jmp loop
.done
   ; place READY prompt?
   rts
}

.end

SAVE "man", start, end
PUTBASIC "launch.bas","launch"
